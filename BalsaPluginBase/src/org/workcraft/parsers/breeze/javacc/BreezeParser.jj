/**
 * JavaCC file
 */
 
options {
  JDK_VERSION = "1.5";
  STATIC = false;
  DEBUG_LOOKAHEAD = false;
  OUTPUT_DIRECTORY = "generated";
}

PARSER_BEGIN(BreezeParser)
package org.workcraft.parsers.breeze.javacc.generated;

import org.workcraft.parsers.breeze.*;
import org.workcraft.parsers.breeze.dom.*;
import org.workcraft.parsers.breeze.expressions.*;

import java.io.InputStream;
import java.util.List;
import java.util.LinkedList;
import java.util.ArrayList;
import java.io.BufferedReader;
import java.io.InputStreamReader;

import pcollections.*;

public class BreezeParser {
	public static LispNode parse (InputStream is) throws ParseException {
		BreezeParser parser = new BreezeParser(new BufferedReader(new InputStreamReader(is)));
		return parser.listElement();
	}

	public static PrimitivePart [] parsePrimitiveParts(InputStream is) throws ParseException
	{
	  BreezeParser parser = new BreezeParser(new BufferedReader(new InputStreamReader(is)));
	  return parser.primitivePartsFile();
	}
 	
	public static BreezeFile parseBreezeFile(InputStream is) throws ParseException
	{
		return new BreezeParser(new BufferedReader(new InputStreamReader(is))).breezeFile();
	}

	ParameterType stringType()
	{
		return ParameterType.Create.string();
	}

	ParameterType cardinalType()
	{
		return ParameterType.Create.cardinal();
	}

	ParameterType namedType(String name)
	{
		if("cardinal".equals(name))
			return ParameterType.Create.cardinal();
		if("boolean".equals(name))
			return ParameterType.Create.bool();
		if("UnaryOperator".equals(name))
			return ParameterType.Create.unaryOp();
		if("BinaryOperator".equals(name))
			return ParameterType.Create.binaryOp();
		throw new org.workcraft.exceptions.NotImplementedException(name+" is not implemented");
	}
	
	Expression<Integer> namedDataType(String name)
	{
		throw new org.workcraft.exceptions.NotImplementedException();
	}
}
PARSER_END(BreezeParser)

SKIP :
{
 	" "
|	"\r"
|	"\t"
|	"\n"
|	<";" (~["\r","\n"])*>
}

TOKEN :
{
  	<PRIMITIVE_PART : "primitive-part" >
    |<ZERO : "0">
  	|<VARIABLE_ARRAY_TYPE : "variable-array-type">
  	|<INTEGER : ("-")?(["0"-"9"])+>
  	|<ACTIVE : "active">
  	|<PASSIVE : "passive">
  	|<INPUT : "input">
  	|<INP : "inp">
  	|<OUTPUT : "output">
  	|<NAMED_TYPE : "named-type">
  	|<NUMERIC_TYPE : "numeric-type">
  	|<BOOL_FALSE :"#f">
 	|<PARAMETERS :  "parameters" >
  	|<PARAM :  "param">
 	|<PORTS :  "ports" >
 	|<PORT :  "port" >
 	|<SYNC_PORT :  "sync-port" >
 	|<ARRAYED_PORT :  "arrayed-port" >
 	|<ARRAYED_SYNC_PORT :  "arrayed-sync-port" >
 	|<SYMBOL :  "symbol" >
 	|<CENTER_STRING :  "centre-string" >
	|<IMPLEMENTATION :  "implementation" > 
	|<TYPE_STRING : "string">	
	|<CASE : "case">
	|<ELSE : "else">
	|<STRING_APPEND : "string-append">
	|<NUMBER_TO_STRING : "number->string">
	|<PLUS : "+">
	
	|<SYNC : "sync">
	|<PUSH : "push">
	|<PULL : "pull">
	|<CHANNELS : "channels">
	|<ATTRIBUTES : "attributes">
	|<COMPONENTS : "components">
	|<COMPONENT : "component">
	|<BREEZE_PART : "breeze-part">
	|<TYPE : "type">
	|<IMPORT : "import">
	
	|<OBR : "(" >
	|<CBR : ")" >
	|<QUOTED_VALUE: "\"" (~["\""])* "\"" >
    |<VALUE: ( ~[" ","\r","\t","\n","(",")","\""] )+ >
}

List<LispNode> listBody() : 
{
	ArrayList<LispNode> result = new ArrayList<LispNode>();
	LispNode o;
}
{
  ( o = listElement() {result.add(o);})*
  {
   return result;
  }
}

LispNode list() : 
{
	List<LispNode> result;
}
{
  <OBR> result = listBody() <CBR>
  {
   return new LispNode(result);
  }
}

String value() : {Token t;}
{
	  t = <VALUE> { return t.image; }
	| t = <QUOTED_VALUE> { return t.image.substring(1, t.image.length()-1); }
	| t = keyword() { return t.image; }
	| t = integerToken() { return t.image; } 
}

LispNode listElement() : { String t; LispNode l;}
{
	  t = value() { return new LispNode(t); }
	| l = list() { return l; } 
}

Token keyword() : {Token t;}
{
  	(t = <PRIMITIVE_PART> 
  	| t = <VARIABLE_ARRAY_TYPE>
  	| t = <ACTIVE>
  	| t = <PASSIVE>
  	| t = <INPUT>
  	| t = <INP>
  	| t = <OUTPUT>
  	| t = <NAMED_TYPE>
  	| t = <NUMERIC_TYPE>
  	| t = <BOOL_FALSE>
 	| t = <PARAMETERS>
  	| t = <PARAM>
 	| t = <PORTS>
 	| t = <PORT>
 	| t = <SYNC_PORT>
 	| t = <ARRAYED_PORT>
 	| t = <ARRAYED_SYNC_PORT>
 	| t = <SYMBOL>
 	| t = <CENTER_STRING>
	| t = <IMPLEMENTATION> 
	| t = <TYPE_STRING>	
	| t = <CASE>
	| t = <ELSE>
	| t = <STRING_APPEND>
	| t = <NUMBER_TO_STRING>
	| t = <PLUS>
	
	| t = <SYNC>
	| t = <PUSH>
	| t = <PULL>
	| t = <CHANNELS>
	| t = <ATTRIBUTES>
	| t = <COMPONENTS>
	| t = <COMPONENT>
	| t = <BREEZE_PART>
	| t = <TYPE>
	| t = <IMPORT>
	)
	
	{ return t; } 	
}

ParameterType parameterType() : { ParameterType result; String typeName; }
{
	(
		LOOKAHEAD(<OBR>(<TYPE_STRING>|<NAMED_TYPE> ))
		<OBR>
		(
			<TYPE_STRING> { result = stringType(); }
			|
			<NAMED_TYPE> 
			(
				typeName = value() { result = namedType(typeName); }
			)
		)
		<CBR>
		| numericType() { result = cardinalType(); }
	)
	{ return result; }
}

ParameterDeclaration parameter() :
{
  String name;
  ParameterType type;
}
{
	<OBR> name = value() type = parameterType() listBody() <CBR>
	{ return new ParameterDeclaration (name, type); }
}

PVector<ParameterDeclaration> parameters() :
{
   PVector<ParameterDeclaration> result = TreePVector.<ParameterDeclaration>empty();
   ParameterDeclaration o;
}
{
 <OBR> <PARAMETERS>
( o = parameter() {result = result.plus(o);})* 
 <CBR>
 { return result; }
}

boolean dataDirectionIsInput() : {}
{
	(<INPUT>|<INP>) { return true; }
	| <OUTPUT> { return false; }
} 

Token integerToken() :
{ Token t; }
{
	(t = <INTEGER> | t = <ZERO>) { return t; }
}

int integer() :
{
	Token t;
}
{
	(t = integerToken())
	{
		return Integer.parseInt(t.image);
	}
}

Expression<Integer> constantIntegerExpression() :
{
	int result;
}
{
	result = integer()
	{ return new Constant<Integer>(result); }
}

Expression<String> constantStringExpression() :
{
	String s;
}
{
	s = value()
 { return new Constant<String>(s); }
}

Expression<?> parameterizedExpression() :
{
	String paramName;
}
{
	<OBR> <PARAM>
		paramName = value()
	<CBR>
	{ return new ParameterReference<Object>(paramName); }
}

Expression<String> numberToStringExpression() :
{ Expression<Integer> intExpression; }
{
	<OBR>
		<NUMBER_TO_STRING>
		intExpression = integerExpression()
	<CBR>
	{ return new ToStringExpression<Integer>(intExpression); }
}

Expression<String> stringAppendExpression() :
{
	Expression<String> result;
	Expression<String> str2;
}
{
	<OBR>
		<STRING_APPEND>
		result = stringAppendBody()
	<CBR>
	{ return result; }
}

Expression<String> stringAppendBody() :
{
	Expression<String> str;
	List<Expression<String>> strs = new ArrayList<Expression<String>>();
}
{
	(str = stringExpression() { strs.add(str); })*
	{ return new StringConcatenateExpression(strs); }
}

Expression<String> stringCaseExpression() :
{
	Expression<String> condition;
	Expression<String> value;
	
	Expression<String> toCheck;
	List<Expression<String>> conditions = new ArrayList<Expression<String>>();
	List<Expression<String>> values = new ArrayList<Expression<String>>();
	Expression<String> elseValue;
}
{
	<OBR>
		<CASE>
		toCheck = stringExpression()
		
		(
			LOOKAHEAD(<OBR><OBR>)
			<OBR>
				<OBR>
					condition = stringExpression()
				<CBR>
				value = stringExpression()
			<CBR>
			{ conditions.add(condition); values.add(value); }
		)*
		<OBR>
			<ELSE>
			elseValue = stringExpression()
		<CBR>
	<CBR>
	{ return new CaseExpression<String, String>(toCheck, conditions, values, elseValue); }
}

Expression<Integer> addExpression() : 
{
	List<Expression<Integer>> args = new ArrayList<Expression<Integer>>();
}
{
	<OBR>
		<PLUS>
		(integerExpression())*
	<CBR>
	{
		return new AddExpression(args);
	}
}


Expression<Integer> integerExpression() :
{
	Expression<?> result;
}
{
	(
		LOOKAHEAD(<OBR><PARAM>)
		result = parameterizedExpression() |
		result = constantIntegerExpression() |
		result = addExpression()
	)
	{return (Expression<Integer>)result;}
}

Expression<String> implicitNumberToString() :
{
	Expression<Integer> res;
}
{
	res = integerExpression()
	{return new ToStringExpression(res);}
} 

Expression<String> stringExpression() :
{
	Expression<?> result;
}
{
	(
		LOOKAHEAD(<OBR><PARAM>) 			result = parameterizedExpression() |
		LOOKAHEAD(<OBR><CASE>)				result = stringCaseExpression() |
		LOOKAHEAD(<OBR> <STRING_APPEND>) 	result = stringAppendExpression() |
		LOOKAHEAD(<OBR> <NUMBER_TO_STRING>)	result = numberToStringExpression() |
		LOOKAHEAD(implicitNumberToString()) result = implicitNumberToString() |
											result = constantStringExpression()
	)

	{return new ToStringExpression((Expression<Object>) result);}
}

Expression<Integer> numericType() :
{
	Expression<Integer> result; 
}
{
	<OBR> <NUMERIC_TYPE> <BOOL_FALSE>
		result = integerExpression()
	<CBR>
	{
		return result;		
	}
}

VariableArrayType variable_array_type() :
{
	Expression<Integer> defaultWidth;
	Expression<Integer> portCount;
	Expression<String> specification;
}
{	
	<OBR> <VARIABLE_ARRAY_TYPE>
	defaultWidth = numericType()
	<ZERO>
	portCount = integerExpression()
	specification = stringExpression()
	<CBR>
	{ return new VariableArrayType(defaultWidth, portCount, specification); }
}

boolean isActive() : {}
{
	<ACTIVE> { return true; }
	| <PASSIVE> { return false; }
}

Expression<Integer> arrayCount() : { Expression<Integer> result; }
{
	<ZERO>
	result = integerExpression()
	{ return result; }
}

PortDeclaration arrayedSyncPort(String name, boolean isActive) :
{
	Expression<Integer> count;
}
{
	count = arrayCount()
	{ return PortDeclaration.createArrayedSync(name, isActive, count); }
}

Expression<Integer> dataType() :
{
	String typeName;
	Expression<Integer> result;
}
{
	LOOKAHEAD(<OBR> <NUMERIC_TYPE>) result = numericType() { return result; }
	| 
	(
		<OBR><NAMED_TYPE>
		typeName = value()
		<CBR>
		{
			return namedDataType(typeName);
		}
	)
}

PortDeclaration arrayedDataPort(String name, boolean isActive) :
{
	Expression<Integer> count;
	Expression<Integer[]> width = null;
	Expression<Integer> simpleWidth = null;
	boolean isInput;
}
{
	isInput = dataDirectionIsInput()
	(
		LOOKAHEAD(<OBR><VARIABLE_ARRAY_TYPE>) width = variable_array_type() | 
			simpleWidth = dataType()
	)
	count = arrayCount()
	{ if(width == null) width = new ConstantArrayType(simpleWidth, count); }
	{ return PortDeclaration.createArrayedData(name, isActive, count, isInput, width); }
}

PortDeclaration simpleSyncPort(String name, boolean isActive) :
{}
{
	{ return PortDeclaration.createSync(name, isActive); }
}

PortDeclaration simpleDataPort(String name, boolean isActive) :
{
	Expression<Integer> width;
	boolean isInput;
}
{


	isInput = dataDirectionIsInput()
	width = dataType()
	{ return PortDeclaration.createData(name, isActive, isInput, width); }
}

PortDeclaration port() :
{ PortDeclaration result; String name; boolean isActive; }
{
	<OBR>

	(
		<PORT>
		name = value()
		isActive = isActive()
		result = simpleDataPort(name, isActive)
	|
		<SYNC_PORT>
		name = value()
		isActive = isActive()
		result = simpleSyncPort(name, isActive)
	| 
 		<ARRAYED_PORT>
		name = value()
		isActive = isActive()
		result = arrayedDataPort(name, isActive)
	|
		<ARRAYED_SYNC_PORT>
		name = value()
		isActive = isActive()
		result = arrayedSyncPort(name, isActive)
	)
	
	listBody()
	<CBR>
	{
		return result; 
	}
}

PVector<PortDeclaration> ports() :
{
	PVector<PortDeclaration> result = TreePVector.<PortDeclaration>empty();
	PortDeclaration o;
}
{
 <OBR> <PORTS>
	( o = port() {result = result.plus(o);})* 
 <CBR>
 	{return result;}
}

Expression<String> symbol() : { Expression<String> str; }
{
	<OBR>
		<SYMBOL>
		<OBR>
			<CENTER_STRING>
			str = stringAppendBody()
		<CBR>
	<CBR>
	{ return str; }
}

PrimitivePart[] primitivePartsFile() :
{
	List<PrimitivePart> result = new ArrayList<PrimitivePart>();
	PrimitivePart part; 
}
{
	(
		(
			  LOOKAHEAD(<OBR><PRIMITIVE_PART>) part = primitivePart() { result.add(part); }
			| listElement()
		)
	)*
	{ return result.toArray(new PrimitivePart[0]); }
}

PrimitivePart primitivePart() :
{
	String name;
	PVector<ParameterDeclaration> parameters;
	Expression<String> symbol;
	PVector<PortDeclaration> ports;
}
{
  <OBR> <PRIMITIVE_PART>
  	name = value()
  	parameters = parameters()
  	ports = ports()
  	symbol = symbol()
  	listBody()
  <CBR>
  { return new PrimitivePart(name, parameters, ports, symbol); }
}

void attributes() : {}
{
	list()
}

ChannelDeclaration channel() :
{
	ChannelType type;
	int bits = 0;
}
{
	<OBR>
	(
		<SYNC> { type = ChannelType.SYNC; }
		| 
		(<PULL> { type = ChannelType.PULL; }
		 | <PUSH> { type = ChannelType.PUSH; })
		bits = integer()
	)
	listBody()
	<CBR>
	{ return new ChannelDeclaration(type, bits); }
}


List<ChannelDeclaration> channels() : 
{
	List<ChannelDeclaration> list = new ArrayList<ChannelDeclaration>();
	ChannelDeclaration channel;
}
{
	<OBR><CHANNELS>
	(channel = channel() { list.add(channel); } )*
	<CBR>
	{ return list; }
}

List<String> paramValues() :
{
	List<String> values = new ArrayList<String>();
	String value;
}
{
	<OBR>
	(value = value() {values.add(value);})*
	<CBR>
	{return values;}
}

List<List<Integer>> connections() : 
{
	List<List<Integer>> result = new ArrayList<List<Integer>>();
	List<Integer> subList;
}
{
	<OBR>
	(subList = intList() {result.add(subList);})*
	<CBR>
	{return result;}
}

List<Integer> intList() :
{
	List<Integer> result;
	int val;
}
{
	(
		result = plainIntList()
		|
		val = integer() { result = new LinkedList<Integer>(); result.add(val); }  
	)
	{ return result; }
} 

List<Integer> plainIntList() : 
{
	List<Integer> result = new ArrayList<Integer>();
	Integer value;
}
{
	<OBR>
	(value = integer() {result.add(value);})*
	<CBR>
	{return result;}
}

RawBreezePartReference component() :
{
	String name;
	List<String> parameters;
	List<List<Integer>> connections;
}
{
	<OBR><COMPONENT>
	name = value()
	parameters = paramValues()
	connections = connections()
	listBody()
	<CBR>
	{
		return new RawBreezePartReference(name, parameters, connections);
	}
}

List<RawBreezePartReference> components() :
{
	List<RawBreezePartReference> list = new ArrayList<RawBreezePartReference>();
	RawBreezePartReference part;
}
{
	<OBR><COMPONENTS>
	(part = component() {list.add(part);})*
	<CBR>
	{return list;}
}

BreezePart breezePart() :
{
	String name;
	List<PortDeclaration> ports;
	List<ChannelDeclaration> channels;
	List<RawBreezePartReference> components;
}
{
	<OBR><BREEZE_PART>
	name = value()
	ports = ports()
	attributes()
	channels = channels()
	components = components()
	listBody()
	<CBR>
	{ return new BreezePart(name, ports, channels, components);}
}

BreezeType breezeType() : {}
{
	<OBR><TYPE>
		listBody()
	<CBR>
	{
	  throw new org.workcraft.exceptions.NotImplementedException();	}
}

String importDef() : { String value; }
{
	<OBR><IMPORT>
	value = value()	<CBR>
	{ return value; }
}

BreezeFile breezeFile() :
{
    String imp;
    BreezeType type;
    BreezePart part;
	List<String> imports = new LinkedList<String>();
	List<BreezeType> typeDefs = new LinkedList<BreezeType>();
	List<BreezePart> partDefs = new LinkedList<BreezePart>();
}
{
	(
		(		  	LOOKAHEAD(<OBR><IMPORT>) imp = importDef() { imports.add(imp); }			| LOOKAHEAD(<OBR><BREEZE_PART>) part = breezePart() { partDefs.add(part); }
			| LOOKAHEAD(<OBR><TYPE>) type = breezeType() { typeDefs.add(type); }
			| listElement()
		)
	)*
	{return new BreezeFile(imports, typeDefs, partDefs);}
}
