buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'ca.coglinc:javacc-gradle-plugin:2.3.1'
        classpath 'de.aaschmid.gradle.plugins:gradle-cpd-plugin:0.5'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}

subprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'ca.coglinc.javacc'
    apply plugin: 'cpd'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'

    sourceCompatibility = 1.7

    sourceSets {
        main {
            java {
                srcDir 'src'
                srcDir 'src-gen'
            }
            resources {
                srcDir 'res'
            }
        }
        test {
            java {
                srcDir 'test'
            }
            resources {
                srcDir 'test'
            }
        }
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:deprecation"
    }

    compileJavacc {
        inputDirectory = file('javacc')
        outputDirectory = file('src-gen')
        include '**/*.jj'
        arguments = [grammar_encoding: 'UTF-8', static: 'false']
    }

    dependencies {
        compile fileTree(dir: '../ThirdParty', include: ['*.jar'])
        compile fileTree(dir: '../ThirdParty/batik', include: ['*.jar'])

        testCompile 'junit:junit:4.5'
    }

    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = "6.16"
        configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    }
    checkstyleMain.exclude '**/jj/**'

    cpd {
        toolVersion = '5.4.1'
        minimumTokenCount = 2000
    }

    cpdCheck {
        reports {
            text.enabled = true
            xml.enabled = false
        }
        source = sourceSets.main.allJava
    }
    cpdCheck.exclude '**/jj/**'

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }

    apply plugin: 'eclipse'
}
