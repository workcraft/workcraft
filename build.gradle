plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'ca.coglinc.javacc'
    id 'com.github.kt3k.coveralls'
    id 'de.aaschmid.cpd'
    id 'com.github.jk1.dependency-license-report'
}

repositories {
    jcenter()
}

subprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'java'
    sourceCompatibility = gradle.ext.javaVersion
    targetCompatibility = gradle.ext.javaVersion

    sourceSets {
        main.java.srcDirs = ['src', 'src-gen']
        main.resources.srcDirs = ['res']
        test.java.srcDirs = ['test-src']
        test.resources.srcDirs = ['test-res']
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:deprecation"
    }

    apply plugin: 'kotlin'

    sourceSets {
        main.kotlin.srcDirs = ['src']
        test.kotlin.srcDirs = ['test-src']
    }

    compileKotlin {
        kotlinOptions.jvmTarget = gradle.ext.javaVersion
    }

    apply plugin: 'ca.coglinc.javacc'

    compileJavacc {
        inputDirectory = file('javacc')
        outputDirectory = file('src-gen')
        include '**/*.jj'
        arguments = [grammar_encoding: 'UTF-8', static: 'false']
    }

    task cleanJavacc(type: Delete) {
        delete compileJavacc.outputDirectory
    }

    configurations {
        libs
    }

    dependencies {
        libs fileTree(dir: 'libs', include: ['**/*.jar'])
        configurations.compile.extendsFrom(configurations.libs)
        testImplementation gradle.ext.junit
        javacc gradle.ext.javacc
    }

    apply plugin: 'eclipse'

    eclipse {
        project {
            buildCommand 'org.jetbrains.kotlin.ui.kotlinBuilder'
            natures 'org.jetbrains.kotlin.core.kotlinNature'
        }
        classpath {
            containers 'org.jetbrains.kotlin.core.KOTLIN_CONTAINER'
        }
    }

    eclipseClasspath.dependsOn compileJavacc

    apply plugin: 'idea'

    idea.module {
        sourceDirs = [file('src'), file('javacc'), file('src-gen')]
        generatedSourceDirs = [file('src-gen')]
        resourceDirs = [file('res'), file('models')]
        testSourceDirs = [file('test-src')]
        testResourceDirs = [file('test-res')]
        excludeDirs = [file('.gradle'), file('.settings'), file('bin'), file('build'), file('gradle')]
        inheritOutputDirs = false
        outputDir = file('build')
    }

    ideaModule.dependsOn compileJavacc

    task cleanAll(dependsOn: ['clean', 'cleanEclipse', 'cleanIdea', 'cleanJavacc'])

    test {
        workingDir = rootDir
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }

    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = gradle.ext.checkstyleVersion
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    }

    checkstyleMain.exclude '**/jj/**'

    task checkstyle(dependsOn: tasks.withType(Checkstyle)*.name)

    apply plugin: 'pmd'

    pmd {
        toolVersion = gradle.ext.pmdVersion
        consoleOutput = true
        incrementalAnalysis = true
        ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml")
        ruleSets = []
    }

    pmdMain.exclude '**/jj/**'

    task pmd(dependsOn: tasks.withType(Pmd)*.name)

    apply plugin: 'de.aaschmid.cpd'

    cpd {
        toolVersion = gradle.ext.pmdVersion
        minimumTokenCount = 250
    }

    cpdCheck {
        source = sourceSets.main.allJava
        exclude '**/jj/**'
    }


    apply plugin: 'jacoco'

    jacoco {
        toolVersion = gradle.ext.jacocoVersion
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = false
        }
    }

    afterEvaluate {project ->
        task copyLibs(type: Copy) {
            from configurations.libs
            into "$buildDir/$gradle.ext.libDirectoryName"
            include '*.jar'
        }

        jar {
            dependsOn copyLibs
            manifest {
                attributes 'Class-Path': configurations.libs.collect {
                    "../$gradle.ext.libDirectoryName/" + it.getName()
                }.join(' ')
            }
        }
    }
}


apply plugin: 'com.github.jk1.dependency-license-report'

import com.github.jk1.license.render.*

licenseReport {
    renderers = [new InventoryHtmlReportRenderer()]
}


cpdCheck {
    source = sourceSets.main.allJava
    exclude '**/jj/**'
}


apply plugin: 'jacoco'

def coverallsProjects = subprojects.findAll {
    it.name ==~ ~(gradle.ext.coverageProjects.join('|'))
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    dependsOn(coverallsProjects.test)

    sourceDirectories.from = coverallsProjects.sourceSets.main.allSource.srcDirs
    additionalSourceDirs.from = coverallsProjects.sourceSets.main.allSource.srcDirs
    classDirectories.from = coverallsProjects.sourceSets.main.output
    executionData.from = coverallsProjects.jacocoTestReport.executionData

    reports {
        html.enabled = true // human readable
        xml.enabled = true // required by coveralls
    }

    afterEvaluate {
        classDirectories.from = classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/jj/**')
        }
    }
}

jacoco {
    toolVersion = gradle.ext.jacocoVersion
}

coveralls {
    sourceDirs = coverallsProjects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
    group = 'Coverage reports'
    dependsOn jacocoRootReport
}

apply plugin: 'idea'

idea.module {
    excludeDirs = [file('.gradle'), file('config'), file('dist-template'), file('doc'), file('gradle')]
    outputDir = file('build')
}

def workcraftRunConfiguration = '''
    <configuration default="false" name="Workcraft" type="Application" factoryName="Application" singleton="true">
        <option name="MAIN_CLASS_NAME" value="org.workcraft.Console" />
        <option name="WORKING_DIRECTORY" value="file://$PROJECT_DIR$" />
        <module name="WorkcraftRunner" />
    </configuration>
'''

idea.workspace.iws {
    withXml { workspace ->
        def runManagerNode = workspace.node.component.find { it.@name == 'RunManager' }
        def workcraftRunNode = runManagerNode.find { it.@name == 'Workcraft' }
        if (workcraftRunNode != null) runManagerNode?.remove(workcraftRunNode)
        runManagerNode?.append(new XmlParser().parseText(workcraftRunConfiguration))
    }
}

tasks.cleanIdea.doLast {
    project.delete 'workcraft.iws'
}

task cleanAll(dependsOn: 'cleanIdea')
