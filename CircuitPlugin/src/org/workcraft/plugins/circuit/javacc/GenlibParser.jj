/**
 * JavaCC file
 */
 
options {
  JDK_VERSION = "1.7";
  LOOKAHEAD = 1;
  STATIC = false;
  DEBUG_PARSER = false;
}

PARSER_BEGIN(GenlibParser)
package org.workcraft.plugins.circuit.javacc;

import java.util.HashSet;
import java.util.HashMap;
import java.util.List;
import java.util.LinkedList;

import org.workcraft.dom.Node;
import org.workcraft.util.Pair;
import org.workcraft.util.Triple;
import org.workcraft.exceptions.InvalidConnectionException;
import org.workcraft.exceptions.FormatException;
import org.workcraft.exceptions.NotFoundException;

import org.workcraft.plugins.circuit.genlib.Library;
import org.workcraft.plugins.circuit.genlib.Gate;
import org.workcraft.plugins.circuit.genlib.Function;

public class GenlibParser {
}
PARSER_END(GenlibParser)

SKIP:
{
    " "
|   "\t"
|   "\r"
|   "\n"
|   <"#" (~["\r", "\n"])*>
}

TOKEN :
{
    <GATE : "GATE">
|   <LATCH : "LATCH">
|   <PIN : "PIN">
|   <INV : "INV">
|   <NONINV : "NONINV">
|   <UNKNOWN : "UNKNOWN">
|   <SEQ : "SEQ">
|   <ANY : "ANY">
|   <ACTIVE_LOW : "ACTIVE_LOW">
|   <ACTIVE_HIGH : "ACTIVE_HIGH">
|   <RISING_EDGE : "RISING_EDGE">
|   <FALLING_EDGE : "FALLING_EDGE">
|   <CONTROL : "CONTROL">
|   <CONSTRAINT : "CONSTRAINT">
|   <ASYNCH : "ASYNCH">
|   <NAME : ["A"-"Z", "a"-"z", "_"] (["A"-"Z", "a"-"z", "_", "0"-"9"])*>
|   <STRING : "\"" (<CHAR> | <ESCAPESEQ>)* "\"">
|   <#CHAR: [" ","!","#"-"[","]"-"~"]> // Printable ASCII characters excluding \ and "
|   <#ESCAPESEQ: "\\" ["\"","\\","n"]> // 2-character sequences \\, \", and \n
|   <ANY_NAME : "*">
|	<NUMERAL: (["-"])?("."(["0"-"9"])+ | (["0"-"9"])+ ("."(["0"-"9"])*)?)>
|   <EXPRESSION: "=" (~[";"])* ";">
}

Library parseGenlib() :
{
    List<Gate> gates; 
}
{
    gates = parseGates()
    {
        return new Library(gates);
    }
}

List<Gate> parseGates():
{
    Gate gate;
    List<Gate> gates = new LinkedList<Gate>();}
{
    (        ( gate = parseGate() | gate = parseLatch() )
        {            gates.add(gate);
        }
    )*
    {        return gates;
    }
}
    
Gate parseGate():
{
    String name;
    Function function;
}
{
    (
        <GATE> 
        name = parseName() <NUMERAL> function = parseFunction()
        (
    	   parsePin()
        )*
    )
    {
        return new Gate(name, function, null, false);
    }
}

Gate parseLatch():
{
    String name;
    Function function;
    String next;
}
{
    (
        <LATCH> 
        name = parseName() <NUMERAL> function = parseFunction()
        (
           parsePin()
        )*
        next = parseSeq()
        [ parseControl() ]
        [ parseConstraint() ]
    )
    {
        return new Gate(name, function, next, false);
    }
}

String parseName():
{
    Token nameToken;
}
{
    (
        nameToken = <NAME> 
    	{
        	return nameToken.image;
    	}
    |   nameToken = <STRING>
	    {
	    	String s = nameToken.image;
    	    return s.substring(1, s.length()-1);
    	}
    )
}

Function parseFunction():
{
    Token nameToken;
    Token expressionToken;
}
{
    nameToken = <NAME> expressionToken = <EXPRESSION>
    {
        String expression = expressionToken.image.replaceAll("^=", "").replaceAll(";$", "");
        return new Function(nameToken.image, expression);
    }
}

void parsePin():
{
}
{
    <PIN> (<NAME> | "*") [<INV>|<NONINV>|<UNKNOWN>] <NUMERAL> <NUMERAL> <NUMERAL> <NUMERAL> <NUMERAL> <NUMERAL>
}

String parseSeq():
{
    Token nextToken = null;
}
{
    <SEQ> <NAME> (nextToken = <NAME> | <ANY>) (<ACTIVE_LOW> | <ACTIVE_HIGH> | <RISING_EDGE> | <FALLING_EDGE> | <ASYNCH>)
    {
        return ((nextToken == null) ? null : nextToken.image);
    } 
}

void parseControl():
{
}
{
    <CONTROL> <NAME> <NUMERAL> <NUMERAL> <NUMERAL> <NUMERAL> <NUMERAL> <NUMERAL>
}


void parseConstraint():
{
}
{
    <CONSTRAINT> <NAME> <NUMERAL> <NUMERAL> 
}

