/**
 * JavaCC file
 */

options {
    JDK_VERSION = "1.8";
    LOOKAHEAD = 1;
    STATIC = false;
    DEBUG_PARSER = true;
    DEBUG_LOOKAHEAD = false;
    DEBUG_TOKEN_MANAGER = false;
}

PARSER_BEGIN(VerilogParser)
package org.workcraft.plugins.circuit.jj.verilog;

import java.util.List;
import java.util.LinkedList;
import java.util.Map;
import java.util.HashMap;

import org.workcraft.plugins.circuit.verilog.VerilogModule;
import org.workcraft.plugins.circuit.verilog.VerilogPort;
import org.workcraft.plugins.circuit.verilog.VerilogAssign;
import org.workcraft.plugins.circuit.verilog.VerilogInstance;
import org.workcraft.plugins.circuit.verilog.VerilogConnection;

public class VerilogParser {
}
PARSER_END(VerilogParser)

SKIP:
{
    " "
|
    "\t"
|
    "\r"
|
    "\n"
|
    <"//" (~["\r", "\n"])*>
|
    <"/*"> : WITHIN_COMMENT
| 
    <"`" (~["\r", "\n"])*>
|
    "specify" : WITHIN_SPECIFY
|
    "primitive" : WITHIN_PRIMITIVE
}

<WITHIN_COMMENT> SKIP:
{
    <"*/"> : DEFAULT
|
    <~[]>
}

<WITHIN_SPECIFY> SKIP:
{
    "endspecify" : DEFAULT
|
     <~[]>
}

<WITHIN_PRIMITIVE> SKIP:
{
    "endprimitive" : DEFAULT
|
     <~[]>
}

TOKEN:
{
    <MODULE : "module">
|
    <ENDMODULE : "endmodule">
|
    <INPUT : "input">
|
    <OUTPUT : "output">
|
    <INOUT : "inout">
|
    <REG : "reg">
|
    <WIRE : "wire">
|
    <ASSIGN : "assign">
|
    <NAME : ["A"-"Z", "a"-"z", "_"] (["A"-"Z", "a"-"z", "_", "0"-"9", "."])*>
|
    <FORMULA: "=" (~[";"])* ";">
|
    <PETRIFY_NAME : ["A"-"Z", "a"-"z", "_", "/"] ([ "A"-"Z", "a"-"z", "_", "0"-"9", ":", "/", "\\", "-"])*>
|
    <STRING : "\"" (<CHAR> | <ESCAPESEQ>)* "\"">
|
    <#CHAR: [" ","!","#"-"[","]"-"~"]> // Printable ASCII characters excluding \ and "
|
    <#ESCAPESEQ: "\\" ["\"","\\","n"]> // 2-character sequences \\, \", and \n
|
    <LOGIC0 : "1'b0">
|
    <LOGIC1 : "1'b1">
|
    <INTEGER : ([ "0"-"9" ])+>
|
    <PETRIFY_ZERO_DELAY : "// This inverter should have a short delay" <WS> <NL>>
|
    <MPSAT_ZERO_DELAY : "// #PRAGMA: zero delay" <WS> <NL>>
|
    <PETRIFY50_INIT_STATE : "// signal values at the initial state:" <WS> <NL> <WS> "//">
|
    <PETRIFY51_INIT_STATE : "// Initial state:" <WS> <NL> <WS> "//">
|
    <MPSAT_INIT_STATE : "// initial values of the signals" <WS> <NL> <WS> "//">
|
     <#WS: ([" ", "\t"])*>
|
     <#NL: (["\n", "\r"])+>
|
    "("
|
    ")"
|
    "["
|
    "]"
|
    ","
|
    "!"
|
    ";"
|
    "."
}

List<VerilogModule> parseCircuit() :
{
    List<VerilogModule> modules;
}
{
    modules = parseModules()
    {
        return modules;
    }
}

List<VerilogModule> parseModules():
{
    VerilogModule module;
    List<VerilogModule> modules = new LinkedList<VerilogModule>();
}
{
    (
        module = parseModule()
        {
            modules.add(module);
        }
    )*
    {
        return modules;
    }
}

VerilogModule parseModule():
{
    String name;
    List<VerilogPort> ports;
    VerilogAssign assign;
    List<VerilogAssign> assigns = new LinkedList<VerilogAssign>();
    VerilogInstance instance;
    List<VerilogInstance> instances = new LinkedList<VerilogInstance>();
    Map<String, Boolean> signalStates = null;
}
{
    <MODULE> name = parseModuleName()
    ports = parsePorts()
    (
        (
            assign = parseAssign()
            {
                assigns.add(assign);
            }
        |
            instance = parseInstance()
            {
                instances.add(instance);
            }
        )
    )*
    [ (<PETRIFY50_INIT_STATE> | <PETRIFY51_INIT_STATE> | <MPSAT_INIT_STATE>)
        signalStates = parseInitialState()
    ]
    <ENDMODULE>
    {
        return new VerilogModule(name, ports, assigns, instances, signalStates);
    }
}

String parseModuleName():
{
    Token nameToken;
}
{
    (nameToken = <NAME> | nameToken = <PETRIFY_NAME>)
    {
        return nameToken.image;
    }
}

List<VerilogPort> parsePorts():
{
    List<VerilogPort> ports;
}
{
    (
        LOOKAHEAD(parseCompactPorts())
        ports = parseCompactPorts()
    |
        ports = parseComplexPorts()
    )
    {
        return ports;
    }
}

List<VerilogPort> parseCompactPorts():
{
    VerilogPort port;
    List<VerilogPort> ports = new LinkedList<VerilogPort>();
}
{
    [
        "("
            (
                port = parseCompactPort() [","]
                {
                    ports.add(port);
                }
            )*
        ")"
    ]
    ";"
    {
        return ports;
    }
}

VerilogPort parseCompactPort():
{
    VerilogPort.Type type;
    VerilogPort.Range range = null;
    Token nameToken;
}
{
    type = parsePortType() [range = parseRange()] nameToken = <NAME>
    {
        return new VerilogPort(nameToken.image, type, range);
    }
}

VerilogPort.Type parsePortType():
{
}
{
    (
        <INPUT>
        {
            return VerilogPort.Type.INPUT;
        }
    |
        <OUTPUT>
        {
            return VerilogPort.Type.OUTPUT;
        }
    )
}

List<VerilogPort> parseComplexPorts():
{
    List<String> declarations;
    List<VerilogPort> definitions;
}
{
    declarations = parsePortsDeclaration()
    definitions = parsePortsDefinitions()
    {
        HashMap<String, VerilogPort> nameToPort = new HashMap<String, VerilogPort>();
        for (VerilogPort port: definitions) {
            nameToPort.put(port.name, port);
        }
        List<VerilogPort> ports = new LinkedList<VerilogPort>();
        for (String name: declarations) {
            VerilogPort port = nameToPort.get(name);
            if (port != null) {
                ports.add(port);
            }
        }
        return ports;
    }
}

List<String> parsePortsDeclaration():
{
    List<String> names = null;
}
{
    ["(" names = parseNames() ")"] ";"
    {
        List<String> ports = new LinkedList<String>();
        if (names != null) {
            ports.addAll(names);
        }
        return ports;
    }
}

List<VerilogPort> parsePortsDefinitions():
{
    List<VerilogPort> ports;
    List<VerilogPort> allPorts = new LinkedList<VerilogPort>();
}
{
    (
        ports = parsePortsDefinition()
        {
            allPorts.addAll(ports);
        }
    )*
    {
        return allPorts;
    }
}

List<VerilogPort> parsePortsDefinition():
{
    VerilogPort.Type type;
    VerilogPort.Range range = null;
    List<String> names;
    List<VerilogPort> ports = new LinkedList<VerilogPort>();
}
{
    (
        <WIRE> [range = parseRange()] parseNames() ";"
    |
        (type = parsePortType() [range = parseRange()] names = parseNames() ";")
        {
            for (String name: names) {
                VerilogPort port = new VerilogPort(name, type, range);
                ports.add(port);
            }
        }
    )
    {
        return ports;
    }
}

VerilogPort.Range parseRange():
{
    Token firstToken;
    Token secondToken;
}
{
    "[" firstToken = <INTEGER> ":" secondToken = <INTEGER> "]"
    {
        Integer first = Integer.parseInt(firstToken.image);
        Integer second = Integer.parseInt(secondToken.image);
        return new VerilogPort.Range(first, second);
    }
}

List<String> parseNames():
{
    Token nameToken;
    List<String> names = new LinkedList<String>();
}
{
    (
        nameToken = <NAME> [","]
        {
            String name = nameToken.image;
            names.add(name);
        }
    )*
    {
        return names;
    }
}

Map<String, Boolean> parseInitialState():
{
    Boolean state;
    Token nameToken;
    Map<String, Boolean> signalStates = new HashMap<String, Boolean>();
}
{
    (
        (
            "!" nameToken = <NAME>
            {
                state = false;
            }
        |
            nameToken = <NAME>
            {
                state = true;
            }
        ) [","]
        {
           String name = nameToken.image;
           signalStates.put(name, state);
        }
    )*
    {
        return signalStates;
    }
}

List<VerilogAssign> parseAssigns():
{
    VerilogAssign assign;
    List<VerilogAssign> assigns = new LinkedList<VerilogAssign>();
}
{
    (
        assign = parseAssign()
        {
            assigns.add(assign);
        }
    )*
    {
        return assigns;
    }
}

VerilogAssign parseAssign():
{
    Token nameToken;
    Token formulaToken;
}
{
    <ASSIGN> ["#"<INTEGER>] nameToken = <NAME> formulaToken = <FORMULA>
    {
        String formula = formulaToken.image
                .replaceAll("^=", "")  // start of assign expression
                .replaceAll(";$", "")  // end of assign expression
                .replace("1'b0", "0")  // LOGIC0
                .replace("1'b1", "1"); // LOGIC1
        return new VerilogAssign(nameToken.image, formula);
    }
}

List<VerilogInstance> parseInstances():
{
    VerilogInstance instance;
    List<VerilogInstance> instances = new LinkedList<VerilogInstance>();
}
{
    (
        instance = parseInstance()
        {
            instances.add(instance);
        }
    )*
    {
        return instances;
    }
}

VerilogInstance parseInstance():
{
    boolean zeroDelay = false;
    String moduleName;
    Token nameToken = null;
    List<String> parameters;
    List<VerilogConnection> connections;
}
{
    [
        (<PETRIFY_ZERO_DELAY> | <MPSAT_ZERO_DELAY>)
        {
            zeroDelay = true;
        }
    ]
    moduleName = parseModuleName()
    [nameToken = <NAME>]
    "(" connections = parseConnections() ")" ";"
    {
        String name = (nameToken == null ? null : nameToken.image);
        return new VerilogInstance(name, moduleName, connections, zeroDelay);
    }
}

List<VerilogConnection> parseConnections():
{
    List<VerilogConnection> connections;
}
{
    (
        LOOKAHEAD(parseNamedConnections())
        connections = parseNamedConnections()
    |
        connections = parseOrderedConnections()
    )
    {
        return connections;
    }
}

List<VerilogConnection> parseNamedConnections():
{
    VerilogConnection connection;
    List<VerilogConnection> connections = new LinkedList<VerilogConnection>();
}
{
    (
        connection = parseNamedConnection()
        {
            connections.add(connection);
        }
    )+
    {
        return connections;
    }
}

VerilogConnection parseNamedConnection():
{
    Token nameToken;
    VerilogConnection wire;
    Integer netIndex = null;
}
{
    "." nameToken = <NAME> "(" wire = parseWire() ")" [","]
    {
        return new VerilogConnection(nameToken.image, wire.netName, wire.netIndex);
    }
}

List<VerilogConnection> parseOrderedConnections():
{
    List<VerilogConnection> connections;
}
{
    connections = parseWires()
    {
        return connections;
    }
}

List<VerilogConnection> parseWires():
{
    VerilogConnection wire;
    List<VerilogConnection> wires = new LinkedList<VerilogConnection>();
}
{
    (
        wire = parseWire() [","]
        {
            wires.add(wire);
        }
    )*
    {
        return wires;
    }
}

VerilogConnection parseWire():
{
    Token nameToken;
    Integer index = null;
}
{
    nameToken = <NAME> [ index = parseIndex()]
    {
        return new VerilogConnection(null, nameToken.image, index);
    }
}

Integer parseIndex():
{
    Token indexToken;
}
{
    "[" indexToken = <INTEGER> "]"
    {
        return Integer.parseInt(indexToken.image);
    }
}
